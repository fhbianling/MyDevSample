import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.bian.mydevsample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 2
        versionName "2.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        multiDexEnabled true
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all {
                variant ->
                    variant.outputs.all { output ->
                        outputFileName = outputFileName.replace(".apk", "-${variant.versionName}-${date()}.apk")
                    }
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled true
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

static String date() {
    def df = new SimpleDateFormat("yyyyMMdd_hhmm")
    println("try run date")
    df.format(System.currentTimeMillis())
}

dependencies {
    api 'androidx.multidex:multidex:2.0.1'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.13.1'
    api project(':lib_base')
    api project(':lib_image')
    implementation project(':lib_media')
    implementation 'com.google.android.material:material:1.2.1'
    api project(':lib_net')

    implementation 'com.wilddog.client:wilddog-sync-android:2.3.7'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // kotlin协程
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'
    // 颜色选择器
    implementation 'com.github.duanhong169:colorpicker:1.1.6'

    // 一个google自己的瀑布流布局容器，这个布局的优点是他的副坐标轴上的排布个数可以不同，不像RecyclerView使用瀑布流时强制每行列数相等这个感觉
    // 这一库同时对应着一个RecyclerView的FlexLayoutManager和一个可以直接使用的控件FlexboxLayout
    implementation 'com.google.android:flexbox:2.0.1'
}
repositories {
    mavenCentral()
}
